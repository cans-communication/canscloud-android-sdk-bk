plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'maven-publish'
}

def crashlyticsEnabled = new File(projectDir.absolutePath +'/google-services.json').exists() && new File(LinphoneSdkBuildDir + '/libs/').exists() && new File(LinphoneSdkBuildDir + '/libs-debug/').exists()

configurations {
    customImplementation.extendsFrom implementation
}
task linphoneSdkSource() {
    doLast {
        configurations.customImplementation.getIncoming().each {
            it.getResolutionResult().allComponents.each {
                if (it.id.getDisplayName().contains("linphone-sdk-android")) {
                    println 'Linphone SDK used is ' + it.moduleVersion.version + ' from ' + it.properties["repositoryName"]
                }
            }
        }
    }
}

project.tasks['preBuild'].dependsOn 'linphoneSdkSource'

android {
    namespace 'com.cans.canscloud_android_sdk'
    compileSdk 32

    defaultConfig {
        minSdk 23
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "library-proguard-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            //signingConfig signingConfigs.release
            consumerProguardFiles 'library-proguard-rules.pro'

            resValue "bool", "crashlytics_enabled", "false"
        }

        releaseWithCrashlytics {
            initWith release

            resValue "bool", "crashlytics_enabled", crashlyticsEnabled.toString()

            if (crashlyticsEnabled) {
                firebaseCrashlytics {
                    nativeSymbolUploadEnabled true
                    unstrippedNativeLibsDir file(LinphoneSdkBuildDir + '/libs-debug/').toString()
                }
            }
        }

        debug {
            jniDebuggable true

            if (crashlyticsEnabled) {
                firebaseCrashlytics {
                    nativeSymbolUploadEnabled true
                    unstrippedNativeLibsDir file(LinphoneSdkBuildDir + '/libs-debug/').toString()
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    def retrofit_version = "2.9.0"
    def logging_version = "4.3.1"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$logging_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_version"
    implementation 'com.squareup.retrofit2:converter-simplexml:2.2.0'

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'

    implementation 'org.linphone:linphone-sdk-android:5.1+'

    implementation "androidx.security:security-crypto-ktx:1.1.0-alpha03"

}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.cans-communication'
            artifactId = 'cansCloud-android-sdk'
            version = '0.3.0'

            afterEvaluate {
                from components.release
            }
        }
    }
}

//ext {
//    GROUP_ID = "io.github.cans-communication"
//    ARTIFACT_ID = POM_ARTIFACT_ID
//    VERSION = VERSION_NAME
//    SITE_URL = 'https://github.com/cans-communication/canscloud-android-sdk/tree/develop'
//    GIT_URL = 'https://github.com/cans-communication/canscloud-android-sdk/tree/develop.git'
//}
//
//
//apply from: 'maven-push.gradle'




